DESCRIPTION = "Telit Ofono library customization"
SECTION = "libs"
LICENSE = "CLOSED"
# LIC_FILES_CHKSUM = ""

PR = "hach"
PV = "1.23+git${SRCPV}"

require ofono.inc

DEPENDS += "python python-pygobject python-dbus"

#!!!!! This version has to be updated if a new commit on fusion_seacloud_ofono.git is done !!!!!
BRANCH = "github_publication_SC4500"
SRCREV="fab00c6d946befc2a4f9c8845eec5bb12c408b2a"

# 0002-Don-t-use-GSM-by-default.patch is a temporary workaround about a hardware issue.
# 0003-Always-update-the-GPRS-status.patch fixes an issue encountered on Eagle systems
# that is probably due to the specific SIM card that is used there.
# 0005-Set-CEMODE-register-of-Multitech-modem-to-2-with-ATT.patch is related to the deactivation
# of 3G technologies on the ATT cellular network.
# 0006-roaming_allowed-now-defaults-to-TRUE.patch enables roaming at ofono level because doing it
# in the modem configuration script is too late and sometimes result in connection failure.
# 0001-Ignore-invalid-DNS-unparsable-or-0.0.0.0-if-any.patch fixes SIM card that returns invalid 0.0.0.0 DNS
# See their commit message for more details.
SRC_URI = "\
	git://github.com/HachCompany-SC4500/fusion_seacloud_ofono.git;branch=${BRANCH} \
	file://0001-Don-t-enforce-Verizon-mode.patch \
	file://0002-Don-t-use-GSM-by-default.patch \
	file://0003-Always-update-the-GPRS-status.patch \
	file://ofono-git-version.patch \
	file://0004-Add-flag-for-linker-for-2.8.patch \
	file://0005-Set-CEMODE-register-of-Multitech-modem-to-2-with-ATT.patch \
	file://0006-roaming_allowed-now-defaults-to-TRUE.patch \
	file://ofono.service \
	file://ofono.sh \
	file://0001-Ignore-invalid-DNS-unparsable-or-0.0.0.0-if-any.patch \
"

S = "${WORKDIR}/git/"

CFLAGS_append_libc-uclibc = " -D_GNU_SOURCE"

do_install_append() {
	# Replace the file ofono.service which has been automatically generated by our custom ofono.service
	cp ${WORKDIR}/ofono.service ${D}${systemd_unitdir}/system/
	# Add ofono.sh
	cp ${WORKDIR}/ofono.sh ${D}${systemd_unitdir}/system/
	install -d ${D}${libdir}/${BPN}/test
	install -m 0755 ${S}/test/* ${D}/${libdir}/${BPN}/test/
}

FILES_${PN} += "${libdir}/*"
